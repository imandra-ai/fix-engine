
module Internal : sig 
  type t
  type event =
    | Internal   of Fix_engine_state.fix_engine_int_out_msg
    | FIXMessage of Full_messages.full_top_level_msg
    | State      of Fix_engine_state.fix_engine_state

  type config = 
    { comp_id        : string
    ; host_id        : string option
    ; on_behalf_id   : string option
    ; target_id      : string
    ; timer          : float
    ; recv           : event -> unit Lwt.t 
    }

  val start : (int * int) -> config:config -> unit Lwt.t * t
  val send_fix : t -> Full_messages.full_top_level_msg -> unit Lwt.t
  val send_int : t -> Fix_engine_state.fix_engine_int_inc_msg -> unit Lwt.t
  val send_getstate : t -> unit Lwt.t

end = struct
  let (>>=) = Lwt.(>>=)

  type send_event =
    | SendInternal   of Fix_engine_state.fix_engine_int_inc_msg
    | SendFIXMessage of Full_messages.full_top_level_msg
    | GetState      

  type event =
    | Internal   of Fix_engine_state.fix_engine_int_out_msg
    | FIXMessage of Full_messages.full_top_level_msg
    | State      of Fix_engine_state.fix_engine_state

  type t = 
    { recv          : event -> unit Lwt.t
    ; send_box      : send_event Lwt_mvar.t
    ; timer         : float
    }

  (** Calls Fix_engine.one_step and pubs outgoing messages while busy *)  
  let rec while_busy_loop t engine_state  =
    let engine_state = Fix_engine.one_step engine_state in
    (* This assumes that after one_step we can get either ontgoing internal or outgoing FIX message *)
    begin match ( engine_state.outgoing_fix_msg , engine_state.outgoing_int_msg) with
      | Some msg, None -> t.recv ( FIXMessage msg )  
      | None, Some msg -> t.recv ( Internal   msg ) 
      | None, None     -> Lwt.return_unit
      |  _ -> Lwt.fail_with "Critical internal error in fix_engine model" 
    end >>= fun () ->
    let engine_state = { engine_state with outgoing_fix_msg = None ; outgoing_int_msg = None } in
    if Fix_engine_state.engine_state_busy engine_state then
      while_busy_loop t engine_state
    else Lwt.return engine_state
  
  let rec main_loop t engine_state =
    let open Fix_engine_state in 
    Lwt_mvar.take t.send_box >>= (function 
    | SendFIXMessage msg -> {engine_state with incoming_fix_msg = Some msg } |> while_busy_loop t
    | SendInternal   msg -> {engine_state with incoming_int_msg = Some msg } |> while_busy_loop t
    | GetState -> t.recv (State engine_state) >>= fun () -> Lwt.return engine_state
    ) >>= fun engine_state -> main_loop t engine_state

  let do_timechange t =
    (** Timechange poster  *)
    let time = Time_defaults_current_time.get_current_utctimestamp () in
    let timechange = Fix_engine_state.IncIntMsg_TimeChange time in
    let timechange = SendInternal timechange in
    Lwt_mvar.put t.send_box timechange
    
  let rec heartbeat_thread t =
    (** Heartbeat thread  *)
    Lwt_unix.sleep t.timer >>= fun () -> 
    do_timechange t >>= fun () -> 
    heartbeat_thread t

  type config = 
    { comp_id        : string
    ; host_id        : string option
    ; on_behalf_id   : string option
    ; target_id      : string
    ; timer          : float
    ; recv           : event -> unit Lwt.t 
    }
  
  let make_engine_state (inseq, outseq) config =
    let open Fix_engine_state in
    { init_fix_engine_state with
      fe_comp_id = config.comp_id
    ; fe_sender_location_id = config.host_id
    ; fe_on_behalf_of_comp_id = config.on_behalf_id
    ; fe_target_comp_id = config.target_id
    ; fe_curr_time = Time_defaults_current_time.get_current_utctimestamp ()
    ; fe_max_num_logons_sent = 10
    ; fe_application_up = true
    ; incoming_seq_num = inseq
    ; outgoing_seq_num = outseq    
    }

  let start (inseq, outsec) ~config  =
    let engine_state = make_engine_state (inseq, outsec) config in
    let state = 
      { recv = config.recv
      ; send_box = Lwt_mvar.create_empty ()
      ; timer = config.timer 
      } in
    let thread = Lwt.pick
      [ heartbeat_thread state
      ; main_loop state engine_state
      ] in
    ( thread , state )

  let send_fix state msg = 
    Lwt_mvar.put state.send_box (SendFIXMessage msg) >>= fun () ->
    do_timechange state

  let send_int state msg =
    Lwt_mvar.put state.send_box (SendInternal msg)

  let send_getstate state =
    Lwt_mvar.put state.send_box GetState


end

include Internal