(** FIX 4.4 engine utility functions. *)
(***
    
    Imandra Inc.
    Copyright (c) 2014 - 2019

    fix_engine_utils.iml
    
*)
[@@@import "../src-core/datetime.iml"]
[@@@import "../src-protocol/full_admin_enums.iml"]
[@@@import "../src-protocol/full_admin_messages.iml"]
[@@@import "../src-protocol/full_messages.iml"]
[@@@import "../src-protocol/full_message_tags.iml"]
[@@@import "../src-protocol/full_admin_tags.iml"]
[@@@import "fix_engine_state.iml"]
[@@@import "../src-core-time-defaults/time_defaults.iml"]
[@@@import "../src-core-utils/fix_version.iml"]
open Datetime;;
open Full_admin_enums;;
open Full_admin_messages;;
open Full_messages;;
open Full_message_tags;;
open Full_admin_tags;;
open Fix_engine_state;;
open Time_defaults;;
open Fix_version;;


(* Does the message have the right sequence number? *)
(** If sequence number is too high, then we need to transfer into Recovery Mode
    and request the missing sequence to be retransmitted. *)
let msg_is_sequence_gap ( engine, msg_header : fix_engine_state * fix_header ) = 
    msg_header.h_msg_seq_num > engine.incoming_seq_num + 1
;;

(** get_gap_fill_msg -> out of all of the administrative messages, only the 'Reject' can be retransmitted.
    All application-level messages may be retransmitted - we should, in the future add logic to 
    not retransmit stale orders, etc. *)
let get_historic_msg ( valid_msg : full_valid_fix_msg ) =  
    let gap_fill_msg_data = Full_FIX_Admin_Msg ( 
        Full_Msg_Sequence_Reset {
            seqr_new_seq_no = valid_msg.full_msg_header.h_msg_seq_num + 1;
            seqr_gap_fill_flag = Some FIX_GapFillFlag_Y;
        }
    ) in

    let gap_fill_msg = {
        full_msg_header = valid_msg.full_msg_header;
        full_msg_data = gap_fill_msg_data;
        full_msg_trailer = valid_msg.full_msg_trailer;
    } in 

    match valid_msg.full_msg_data with 
    | Full_FIX_Admin_Msg admin_msg ->
        begin
            match admin_msg with 
            | Full_Msg_Reject _m -> valid_msg
            | _ -> gap_fill_msg
        end
    | Full_FIX_App_Msg _app_msg -> valid_msg
;; 

(** This function returns a new sequence number if we got a SequenceReset without GapFill flag.
    Returns None in all other cases.  *)
let get_critical_reset_seq_num (msg_data : full_msg_data ) =
    match msg_data with
    | Full_FIX_Admin_Msg (Full_Msg_Sequence_Reset data) -> begin
        match data.seqr_gap_fill_flag with 
        | Some FIX_GapFillFlag_Y -> None
        | _ -> Some data.seqr_new_seq_no
        end
    | _ -> None
;;

(** Give us a single GapFill message with the correct NextSequenceNumber.

   From the specification:
    "If there are consecutive administrative messages to be resent, it is suggested that only one SeqReset-GapFill
    message be sent in their place. The sequence number of the SeqReset-GapFill message is the next expected
    outbound sequence number. The NewSeqNo field of the GapFill message contains the sequence number of
    the highest administrative message in this group plus 1. For example, during a Resend operation there are 7
    sequential administrative messages waiting to be resent. They start with sequence number 9 and end with
    sequence number 15. Instead of transmitting 7 Gap Fill messages (which is perfectly legal, but not network
    friendly), a SeqReset-GapFill message may be sent. The sequence number of the Gap Fill message is set to 9
    because the remote side is expecting that as the next sequence number. The NewSeqNo field of the GapFill
    message contains the number 16, because that will be the sequence number of the next message to be
    transmitted."  *)
let combine_gapfill_msgs ( msgOne, msgTwo : full_msg_sequence_reset_data * full_msg_sequence_reset_data ) =
    let correct_next_seq = if msgOne.seqr_new_seq_no > msgTwo.seqr_new_seq_no then msgOne.seqr_new_seq_no else msgTwo.seqr_new_seq_no in {
        seqr_gap_fill_flag = Some FIX_GapFillFlag_Y; (* We assume all of these SeqResetMessages are GapFill-type *)
        seqr_new_seq_no = correct_next_seq;
    }
;;

(** 
    Adding a valid message to the list, while
    - converting any message that needs to be converted into a SequenceRest-GapFill
    - ensuring that any two sequential SequenceReset-GapFill messages are combined into one with an updated
        expected NextSeqNum parameter *)
let add_msg_to_history ( history, msg : full_valid_fix_msg list * full_valid_fix_msg ) = 
    let hist_msg = get_historic_msg ( msg ) in 
    match history with
    | [] -> [ hist_msg ]
    | x::xs ->
        begin
            match x.full_msg_data, hist_msg.full_msg_data with 
            | Full_FIX_Admin_Msg ( Full_Msg_Sequence_Reset seq_reset_one ), Full_FIX_Admin_Msg ( Full_Msg_Sequence_Reset seq_reset_two ) ->
                begin
                    let new_msg_data = combine_gapfill_msgs ( seq_reset_one, seq_reset_two ) in
                    let new_full_msg = {
                        full_msg_header = x.full_msg_header;
                        full_msg_data = Full_FIX_Admin_Msg ( Full_Msg_Sequence_Reset (new_msg_data));
                        full_msg_trailer = x.full_msg_trailer;
                    } in 
                    new_full_msg :: xs
                end 
            | _ -> hist_msg :: x :: xs
        end
;;


(** Create outbound FIX message with the appropriate header and trailer. *)
let create_outbound_fix_msg ( engine, msg, is_duplicate ) = {
    full_msg_header = {
        h_begin_string = default_session_details.constant_begin_string;
        h_body_length = 0;
        h_msg_seq_num = engine.outgoing_seq_num + 1;
        h_poss_dup_flag = if is_duplicate then Some is_duplicate else None;
        h_target_comp_id = engine.fe_target_comp_id;
        h_sender_comp_id = engine.fe_comp_id;
        h_sending_time   = engine.fe_curr_time;
        h_on_behalf_of_comp_id = engine.fe_on_behalf_of_comp_id;
        h_deliver_to_comp_id   = engine.fe_deliver_to_comp_id;
        h_secure_data_len                 = None;
        h_secure_data                     = None;
        h_sender_sub_id                   = None;
        h_sender_location_id              = None;
        h_target_sub_id                   = None;
        h_target_location_id              = None;
        h_on_behalf_of_sub_id             = None;
        h_on_behalf_of_location_id        = None;
        h_deliver_to_sub_id               = None;
        h_deliver_to_location_id          = None;
        h_poss_resend                     = None;
        h_orig_sending_time               = None;
        h_xml_data_len                    = None;
        h_xml_data                        = None;
        h_message_encoding                = None;
        h_last_msg_seq_num_processed      = None;
        h_no_hops                         = None;
    };
    
    (* We're simply attaching the message data here. *)
    full_msg_data = msg;

    (* Trailers would be augmented by raw OCaml printers/parsers. *)
    full_msg_trailer = {
        signature_length = None;
        signature        = None;
        check_sum        = 0;
    }
};;

(** Create a logon message we would send out to initiate a connection 
    with another FIX engine. *)
let create_logon_msg ( engine, reset_seq_num : fix_engine_state * bool ) = 
    let msg_data = Full_FIX_Admin_Msg ( 
        Full_Msg_Logon {
            ln_encrypt_method               = engine.fe_encrypt_method;
            ln_heartbeat_interval           = engine.fe_heartbeat_interval;
            ln_raw_data_length              = None; 
            ln_raw_data                     = None;
            ln_reset_seq_num_flag           = if reset_seq_num then Some true else None;
            ln_next_expected_msg_seq_num    = None;
            ln_max_message_size             = None;

            ln_test_message_indicator       = None;
            ln_username                     = None;
            ln_password                     = None;

            ln_msg_types                    = []
        } 
    ) in 
    create_outbound_fix_msg (engine, msg_data, false ) 
;;

(** Create a Logoff message. *)
let create_logoff_msg ( engine : fix_engine_state ) = 
    let msg_data = Full_FIX_Admin_Msg (
        Full_Msg_Logoff {
            lo_text                 = None;
            lo_encoded_text_len     = None;
            lo_encoded_text         = None;
        }
    ) in 
    create_outbound_fix_msg (engine, msg_data, false ) 
;;

(** Create a heartbeat message *)
let create_heartbeat_msg ( engine, tr_id : fix_engine_state * string option) =
    let msg_data = Full_FIX_Admin_Msg (
        Full_Msg_Heartbeat {
            hb_test_req_id = tr_id;
        }
    ) in 
    create_outbound_fix_msg (engine, msg_data, false ) 
;;

(** Create Test Request message. *)
let create_test_request_msg ( engine : fix_engine_state ) =
    let msg_data = Full_FIX_Admin_Msg (
        Full_Msg_Test_Request {
            test_req_id = engine.last_test_req_id;
        }
    ) in
    create_outbound_fix_msg (engine, msg_data, false )  
;;

(** Create Resend Request message. *)
let create_resend_request_msg ( engine : fix_engine_state ) =
    let msg_data = Full_FIX_Admin_Msg (
        Full_Msg_Resend_Request {
             rr_begin_seq_num = engine.incoming_seq_num;
             rr_end_seq_num   = 0  (*  (represents infinity)  *)            
        }
    ) in
    create_outbound_fix_msg (engine, msg_data, false ) 
;;



(** Create session-rejection message. *)
let create_session_reject_msg ( engine , reject_info ) = 
    let msg_data = 
        Full_FIX_Admin_Msg (
            Full_Msg_Reject {
                sr_ref_seq_num              = reject_info.srej_msg_msg_seq_num;
                sr_ref_tag_id               = reject_info.srej_msg_field_tag;
                sr_ref_msg_type             = reject_info.srej_msg_msg_type;
                sr_session_reject_reason    = reject_info.srej_msg_reject_reason;
                sr_text                     = reject_info.srej_text;
                sr_encoded_text_len         = reject_info.srej_encoded_text_len;
                sr_encoded_text             = reject_info.srej_encoded_text
            } ) in
    create_outbound_fix_msg ( engine , msg_data, false )
;;

(** Create business reject message.
    Note: the reason we're separating the ApplicationDown reason is that the parser would not have access to this information. *)
let create_business_reject_msg ( engine , reject_info  ) =
    let msg_data = 
        Full_FIX_Admin_Msg (
            Full_Msg_Business_Reject {
                br_ref_seq_num            = reject_info.brej_msg_ref_seq_num;       
                br_business_reject_reason = reject_info.brej_msg_reject_reason;
                br_ref_msg_type           = reject_info.brej_msg_msg_tag;
                br_ref_field_id           = reject_info.brej_msg_field_tag;
                br_text                   = reject_info.brej_msg_text;
                br_encoded_text_len       = None; 
                br_encoded_text           = None 
            }
    ) in 
    create_outbound_fix_msg ( engine , msg_data, false )
;;

(*** ********************************************************************************************************** *)
(***  Mode transition functions.                                                                                *)
(*** ********************************************************************************************************** *)

(** A NO-OPeration *)
let noop ( _m, engine : full_valid_fix_msg * fix_engine_state ) = { 
    engine
        with incoming_fix_msg = None
}
;;

(** Convert the rejection information into an outbound BusinessReject message. *)
let business_reject ( rejected_data, engine : biz_rejected_msg_data * fix_engine_state ) =
    let reject_msg = create_business_reject_msg ( engine , rejected_data ) in 
    {   engine with
            incoming_fix_msg        = None;
            fe_last_time_data_sent  = engine.fe_curr_time;
            outgoing_fix_msg        = Some ( ValidMsg ( reject_msg ));
            incoming_seq_num        = rejected_data.brej_msg_ref_seq_num;
            outgoing_seq_num        = engine.outgoing_seq_num + 1;
            fe_history              = add_msg_to_history ( engine.fe_history, reject_msg );
        }
;;

(** Convert the rejection information into an outbound Reject message. *)
let session_reject ( rejected_data, engine : session_rejected_msg_data * fix_engine_state ) =
    (** It is unclear what one should do with session rejected messages that are off-sequence-number 
    the compromise is to increment the sequence number if it is correct and ignore otherwise*)
    let inseq = 
        if rejected_data.srej_msg_msg_seq_num = engine.incoming_seq_num + 1 
        then rejected_data.srej_msg_msg_seq_num
        else engine.incoming_seq_num
        in 
    let reject_msg = create_session_reject_msg ( 
        engine , rejected_data 
    ) in {
        engine with 
            incoming_fix_msg        = None;
            fe_last_time_data_sent  = engine.fe_curr_time;
            outgoing_fix_msg        = Some ( ValidMsg ( reject_msg ));
            incoming_seq_num        = inseq; 
            outgoing_seq_num        = engine.outgoing_seq_num + 1;
            fe_history              = add_msg_to_history ( engine.fe_history, reject_msg );
        }
;;


let field_null ( f : int option ) =
    match f with 
    | None      -> true
    | Some x    -> x = 0
;;

let hbeat_interval_null ( interval : fix_duration ) =
     ( interval.dur_years = 0)   &&
     ( interval.dur_months = 0)  && 
     ( interval.dur_days = 0)    && 
     ( interval.dur_hours = 0)   &&
     ( interval.dur_minutes = 0) &&
     ( interval.dur_seconds = 0)
;;


(** Check the message header ( presence and value of the OrigSendingTime ) 
    returns None if no problems are found. *)
let validate_message_header ( engine, msg_header, msg_tag : fix_engine_state * fix_header * full_msg_tag ) = 
    let curr_incoming_seq_num = engine.incoming_seq_num in 
    let reject = { (* No orig_sending_time => create session reject *)
            srej_msg_msg_seq_num   = msg_header.h_msg_seq_num;
            srej_msg_field_tag     = None;
            srej_msg_msg_type      = Some msg_tag;
            srej_msg_reject_reason = None;
            srej_text              = None; 
            srej_encoded_text_len  = None;
            srej_encoded_text      = None;
        } in
    match msg_header.h_poss_dup_flag with Some false | None -> None | Some true ->
    (* Message header has a PossibleDuplicate flag => must have orig_sending_time *)
    match msg_header.h_orig_sending_time with 
    | None -> 
        let reject =  { reject with 
            srej_msg_field_tag = Some (Full_Admin_Field_Tag Full_Msg_OrigSendingTime_Tag);
            srej_msg_reject_reason = Some RequiredTagMissing; 
            (* TODO: rejection fix_string here *)
            } in
        let engine = session_reject ( reject , engine ) in
        Some { engine with incoming_seq_num = curr_incoming_seq_num }
    | Some orig_sending_time ->
    if utctimestamp_LessThan  msg_header.h_sending_time  orig_sending_time   then
        let reject = { reject with
            srej_msg_field_tag = Some (Full_Admin_Field_Tag Full_Msg_OrigSendingTime_Tag);
            srej_msg_reject_reason = Some SendingTimeAccuracyProblem;
            } in
        let engine = session_reject ( reject , engine ) in
        Some { engine with fe_curr_mode = ShuttingDown }
    else None
;;
